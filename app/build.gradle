apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'me.ele.mess'
apply plugin: 'io.objectbox'

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)

def stringType = 'String'
def clientTokenName = 'CLIENT_TOKEN'
def clientToken = properties.getProperty('client.token')

def signFile = properties.getProperty('key.file')
def signAlias = properties.getProperty('key.alias')
def signPassword = properties.getProperty('key.password')
def signAliasPassword = properties.getProperty('key.alias.password')

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.2"
  defaultConfig {
    applicationId "top.saplf.designerfun"
    minSdkVersion 21
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
    externalNativeBuild {
      cmake {
        cppFlags ''
      }
    }
  }

  signingConfigs {
    appSignConfig {
      storeFile file(signFile)
      storePassword signPassword
      keyAlias signAlias
      keyPassword signAliasPassword
    }
  }

  buildTypes {
    all {
      buildConfigField stringType, clientTokenName, "\"$clientToken\""
      signingConfig signingConfigs.appSignConfig
    }

    debug {
      kapt {
        arguments {
          arg('objectbox.debug', true)
        }
      }
    }

    release {
      debuggable false
      minifyEnabled true
      zipAlignEnabled true
      shrinkResources true
      //proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-common.pro'
      proguardFile 'proguard-rules.pro'
    }
  }

  externalNativeBuild {
    cmake {
      path 'CMakeLists.txt'
    }
  }
}

dependencies {
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

  implementation fileTree(dir: 'libs', include: ['*.jar'])

  def supportVersion = '26.1.0'
  implementation "com.android.support:recyclerview-v7:$supportVersion"
  implementation "com.android.support:appcompat-v7:$supportVersion"
  implementation "com.android.support:design:$supportVersion"
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

  def okhttpVersion = '3.9.0'
  implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

  // en-de json
  implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

  // rx series
//  implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

  def retrofitVersion = '2.3.0'
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
//  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
  implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.8.2'

  // log
  implementation 'com.orhanobut:logger:2.1.1'

  // this rep is used for intent & bundle data's transfer
  implementation 'me.eugeniomarletti:android-extras-delegates:1.0.5'

  // smooth animation
  implementation 'at.wirecube:additive_animations:1.3.1'

  // kotlin coroutine
  def coroutineVersion = '0.19.1'
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

  // Glide relative
  def glideVersion = '4.1.1'
  implementation "com.github.bumptech.glide:glide:$glideVersion"
  implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
  kapt "com.github.bumptech.glide:compiler:$glideVersion"
  implementation 'jp.wasabeef:glide-transformations:3.0.1' // glide transformation
//  implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1' // gpu

  // objectBox
  implementation "io.objectbox:objectbox-android:$objectboxVersion"
//  debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectboxVersion"
  implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
}

kotlin {
  experimental {
    coroutines "enable"
  }
}

mess {
//    ignoreProguard 'com.jakewharton:butterknife'
  ignoreProguard 'com.android.support:appcompat-v7'
  ignoreProguard 'com.android.support:recyclerview-v7'
}
